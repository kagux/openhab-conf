val logName = "bedroom_entrance.rules"

// BRIGHTNESS constants
val Number dayBrightness = 90
val Number nightBrightness = 20

rule "Bedroom Entrance: turn ON lights on bendroom entrance motion"
when  
    Item Bedroom_Entrance_Motion_1 changed to ON or 
    Item Bedroom_Entrance_Motion_2 changed to ON
then
    logInfo("bedroom_entrance.rules", "Detected motion in Bedroom Entrance")
    Bedroom_Entrance_Lights_Switch.sendCommand(ON)
end

rule "Bedroom Entrance: dim light after power reset"
when
    Item Bedroom_Entrance_Lights_Dimmer changed
then 
    if ((Bedroom_Entrance_Lights_Dimmer.state as Number).intValue == 100) {
        logInfo("bedroom_entrance.rules", "Detected 100 brightness, probably after power reset. Dimming")
        Bedroom_Entrance_Lights_Switch.sendCommand(ON)
    }
end

rule "Bedroom Entrance: set brightness"
when
    Item Bedroom_Entrance_Lights_Switch received command
then
    if (receivedCommand != ON) {
        return
    }
    switch(vTimeOfDay.state) {
        case "NIGHT",
        case "BED": {
            Bedroom_Entrance_Lights_Dimmer.sendCommand(nightBrightness)
        }
        default: {
            Bedroom_Entrance_Lights_Dimmer.sendCommand(dayBrightness)
        }
    }
end