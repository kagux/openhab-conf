val logName = "hallway.rules"

val Number dayBrightness = 80

rule "Hallway: turn ON lights on motion"
when  
    Item Hallway_Motion changed to ON
then
    logInfo(logName, "Detected motion in Hallway")
    Hallway_Lights_Switch_Proxy.sendCommand(ON)
end

rule "Hallway: turn ON lights when Exit door opened"
when
    Item Hallway_Exit_Door changed to OPEN
then 
    logInfo(logName, "Hallway exit door opened")
    Hallway_Lights_Switch_Proxy.sendCommand(ON)
end

rule "Hallway: dim light after power reset"
when
    Item Hallway_Lights_Dimmer changed
then 
    if ((Hallway_Lights_Dimmer.state as Number).intValue == 100) {
        logInfo(logName, "Detected 100 brightness, probably after power reset. Dimming")
        Hallway_Lights_Switch_Proxy.sendCommand(ON)
    }
end

rule "Hallway: forward proxy switch"
when
    Item Hallway_Lights_Switch_Proxy received command
then
    logInfo(logName, "Time of day is " + vTimeOfDay.state)
    switch(vTimeOfDay.state) {
        case "DAY",
        case "AFTERNOON": {
            if (Hallway_Luminance.state < 10) {
                logInfo(logName, "It's dark, turning lights ON")
                Hallway_Lights_Long_Switch_Proxy.sendCommand(receivedCommand)
            }
            logInfo(logName, "It's bright, keeping lights OFF")
        }
        case "MORNING",
        case "EVENING": {
            logInfo(logName, "Using long switch to keep lights ON")
            Hallway_Lights_Long_Switch_Proxy.sendCommand(receivedCommand)
        }
        case "NIGHT",
        case "BED": {
            logInfo(logName, "Using long switch to keep lights ON")
            Hallway_Lights_Short_Switch_Proxy.sendCommand(receivedCommand)
        }
    }
end

rule "Hallway: forward short and long proxy switch commands"
when
    Item Hallway_Lights_Short_Switch_Proxy received command
    or 
    Item Hallway_Lights_Long_Switch_Proxy received command
then
    Hallway_Lights_Switch.sendCommand(receivedCommand)
    if (receivedCommand == ON) {
        Hallway_Lights_Dimmer.sendCommand(dayBrightness)
    }
end