val logName = "hallway.rules"
val Number dayBrightness = 60
val Number nightBrightness = 20

rule "Xiaomi default values"
when
    System started
then
    Entrance_MotionTimer_1.sendCommand(30)
    Hallway_MotionTimer_1.sendCommand(30)
end

rule "Hallway: turn ON lights on motion"
when  
    Item Hallway_Motion changed to ON or
    Item Hallway_Motion_1 changed to ON or
    Item Entrance_Motion_1 changed to ON
then
    logInfo(logName, "Detected motion in Hallway")
    Hallway_Lights_Switch_Proxy.sendCommand(ON)
end

rule "Hallway: turn ON lights when Exit door opened"
when
    Item Hallway_Exit_Door changed to OPEN
then 
    logInfo(logName, "Hallway exit door opened")
    Hallway_Lights_Switch_Proxy.sendCommand(ON)
end

rule "Hallway: dim light after power reset"
when
    Item Hallway_Lights_Dimmer changed
then 
    if ((Hallway_Lights_Dimmer.state as Number).intValue == 100) {
        logInfo(logName, "Detected 100 brightness, probably after power reset. Dimming")
        Hallway_Lights_Switch_Proxy.sendCommand(ON)
    }
end

rule "Hallway: forward proxy switch"
when
    Item Hallway_Lights_Switch_Proxy received command ON
then
    logInfo(logName, "Time of day is " + vTimeOfDay.state)
    switch(vTimeOfDay.state) {
        case "MORNING",
        case "DAY",
        case "AFTERNOON": {
            if (Hallway_Luminance.state < 10) {
                logInfo(logName, "It's dark, turning lights ON")
                Hallway_Lights_Switch.sendCommand(receivedCommand)
            }
            logInfo(logName, "It's bright, keeping lights OFF")
        }
        case "EVENING",
        case "NIGHT",
        case "BED": {
            Hallway_Lights_Switch.sendCommand(receivedCommand)
        }
    }
end

rule "Hallway: set brightness"
when
    Item Hallway_Lights_Switch received command
then
    if (receivedCommand != ON) {
        return
    }
    switch(vTimeOfDay.state) {
        case "NIGHT",
        case "BED": {
            Hallway_Lights_Dimmer.sendCommand(nightBrightness)
        }
        default: {
            Hallway_Lights_Dimmer.sendCommand(dayBrightness)
        }
    }
end